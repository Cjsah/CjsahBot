plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

archivesBaseName = rootProject.name

dependencies {
    implementation project(":api")
    implementation("org.java-websocket:Java-WebSocket:$websocket_version") {
        exclude group: 'org.slf4j'
    }
    implementation("org.quartz-scheduler:quartz:$quartz_version") {
        exclude group: 'com.zaxxer'
        exclude group: 'com.mchange'
        exclude group: 'org.slf4j'
    }
}

tasks.register('copyJar', Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/lib'
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('docJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

jar.dependsOn([copyJar])

jar {
    from("LICENSE")
    manifest {
        attributes([
                "Main-Class": "net.cjsah.bot.Main",
                "Class-Path": configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
        ])
    }
}

tasks.register('runMain', JavaExec) {
    group = "application"
    description = "Runs the main class"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'net.cjsah.bot.Main'

    File runDir = rootProject.file("run")

    // 在任务执行之前检查并创建工作目录
    doFirst {
        if (!runDir.exists()) {
            runDir.mkdirs()
        }
    }

    // 设置运行目录
    workingDir = runDir

    // 如果需要传递 JVM 参数或程序参数，可以在这里添加
    // jvmArgs = listOf("-Xmx512m")
    // args = listOf("arg1", "arg2")
}
repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java
            artifact sourcesJar
            artifact docJar
        }
    }

    def ENV = System.getenv()
    if (ENV.MAVEN_URL) {
        repositories.maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}